10 DIM CMD$(2),LINE$(100),SLIN(30):SL=0:SLIN(0)=0
20 GRAPHICS 0:POKE 710,160:POKE 709,0:POKE 752,1:? " "
30 READ CMD$
40 IF CMD$="S" THEN GOSUB 100
50 IF CMD$="L" THEN GOSUB 200
60 IF CMD$="LP" THEN GOSUB 400
70 IF CMD$="Q" THEN GOSUB 300
80 GOTO 30
100 IF SL>0 THEN GOSUB 900
105 FOR I=15 TO 0 STEP -1:POKE 709,I:FOR P=0 TO 5:NEXT P:NEXT I
110 SL=SL+1:SLIN(SL)=PEEK(183)+256*PEEK(184):READ LINE$:L=LEN(LINE$)
120 ? CHR$(125):POSITION INT((38-L)/2),1
130 ? CHR$(160);:FOR I=1 TO L:? CHR$(ASC(LINE$(I,I))+128);:NEXT I:? CHR$(160):? :? :? 
135 FOR I=0 TO 15:POKE 709,I:FOR P=0 TO 5:NEXT P:NEXT I
140 RETURN 
200 READ LINE$:? "* ";LINE$:? :RETURN 
300 GOSUB 900:GRAPHICS 0:END 
400 GOSUB 200:GOSUB 900:RETURN 
900 POKE 764,255
910 IF PEEK(764)=255 THEN 910
915 IF PEEK(764)=6 THEN SL=SL-1:RESTORE SLIN(SL):IF CMD$="S" THEN READ CMD$
920 RETURN 
